/// version = 0.1
/// title = "nNGM: Operation Therapie - FHIR to CTS"

// Art der Resektion, Resektionstatus not mapped in to FHIR Map due to collate bug


map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_OperationTherapieCTS" = nNGM_Mapping_OperationTherapieCTS

uses "http://hl7.org/fhir/StructureDefinition/Bundle" as source
uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as target

group TransformOperationTherapieCTS(source src: Bundle, target tgt: BackboneElement)
{
    src -> tgt.crfid = 'OP1';
    src -> tgt.type = 'save';

    src.entry as entry then
    {
        /* ------------------------------ Procedure ---------------------------- */
        entry.resource as procedure where "resource is Procedure and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Procedure/nNGM/OP'" then
        {
            // Operationsdatum
            procedure -> tgt.data as data then
            {
                procedure.performedDateTime as performedDateTime then
                {
                    procedure -> data.blockindex = 0;
                    procedure -> data.groupindex = 0;
                    procedure -> data.itemid = 'id_842';
                    performedDateTime -> data.values as values, values.value = performedDateTime;
                };
            };
            
            // DurchfÃ¼hrende Einrichtung
            procedure -> tgt.data as data then
            {
                procedure.performer as performer, performer.actor as actor, actor.display as display then
                {
                    procedure -> data.blockindex = 0;
                    procedure -> data.groupindex = 0;
                    procedure -> data.itemid = 'id_1162';
                    display -> data.values as values, values.value = display;
                };  
            };
            
            // Operative Intention
            procedure -> tgt.data as data then
            {
                procedure.reasonCode as reasonCode, reasonCode.coding as coding where "coding.system = 'http://uk-koeln.de/fhir/CodeSystem/nngm/intention-therapie'" then
                {
                    procedure -> data.blockindex = 1;
                    procedure -> data.groupindex = 0;
                    procedure -> data.itemid = 'id_1167';

                    // Kurativ
                    coding.code as code where "code = 'Kurativ'" then
                    {
                        procedure -> data.values as values, values.value = 'Kurativ';
                    };

                    // Palliativ
                    coding.code as code where "code = 'Palliativ'" then
                    {
                        procedure -> data.values as values, values.value = 'Palliativ';
                    };

                    // Diagnostisch
                    coding.code as code where "code = 'Diagnostisch'" then
                    {
                        procedure -> data.values as values, values.value = 'Diagnostisch';
                    };

                    // Revision/Komplikation
                    coding.code as code where "code = 'Revision\/Komplikation'" then
                    {
                        procedure -> data.values as values, values.value = 'Revision\/Komplikation';
                    };

                    // Sonstiges
                    coding.code as code where "code = 'Sonstiges'" then
                    {
                        procedure -> data.values as values, values.value = 'Sonstiges';
                    };

                    // N/A
                    coding.code as code where "code = 'X'" then
                    {
                        procedure -> data.values as values, values.value = 'N\/A';
                    };
                };
            };

            // Art des Eingriffs
            procedure -> tgt.data as data then
            {
                procedure.code as code, code.extension as extension then
                {
                    extension.valueCodeableConcept as vcc, vcc.coding as coding then
                    {
                        coding.code as code where "code != 'unknown'" then 
                        {
                            procedure -> data.blockindex = 1;
                            procedure -> data.groupindex = 0;
                            procedure -> data.itemid = 'id_1168';
                            coding.code as code -> data.values as values, values.value = code;
                        };
                    };

                    extension.valueCodeableConcept as vcc, vcc.coding as coding then
                    {
                        coding.code as code where "code = 'unknown'" then
                        {
                            procedure -> data.blockindex = 1;
                            procedure -> data.groupindex = 0;
                            procedure -> data.itemid = 'id_1168';
                            procedure -> data.values as values, values.value = 'N\/A';
                        };
                    };
                };
            };

            // Lokalisation (Auswahl)
            procedure -> tgt.data as data then
            {
                procedure.bodySite as bodySite, bodySite.coding as coding where "coding.system = 'http://uk-koeln.de/fhir/ValueSet/nNGM/OperationLokalisationUNK'" then
                {
                    procedure -> data.blockindex = 1;
                    procedure -> data.groupindex = 0;
                    procedure -> data.itemid = 'id_2335';
                    coding.code as code -> data.values as values, values.value = code;                   
                };

                procedure.bodySite as bodySite, bodySite.coding as coding where "coding.system = 'http://ncit.nci.nih.gov'" then
                {
                    coding.code as code where "code = 'C17649'" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_2335';
                        procedure -> data.values as values, values.value = 'other';
                    
                        procedure.bodySite as bodySite, bodySite.text as text where "%tgt.operations.data.values.where(isother = 'true').exists().not()" then
                        {
                            procedure -> data.blockindex = 1;
                            procedure -> data.groupindex = 0;
                            procedure -> data.itemid = 'id_2335';
                            text -> data.values as values, values.isother = true, values.value = text;
                        };
                    };
                };

                procedure.bodySite as bodySite, bodySite.coding as coding where "coding.system = 'http://terminology.hl7.org/CodeSystem/data-absent-reason'" then
                {
                    coding.code as code where "code = 'unknown'" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_2335';
                        procedure -> data.values as values, values.value = 'N\/A';
                    };
                };
            };
            
            // Seitenlokalisation
            procedure -> tgt.data as data then
            {
                procedure.bodySite as bodySite, bodySite.extension as extension where "extension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/seitenlokalisation'" then
                {
                    extension.valueCodeableConcept as vcc, vcc.coding as coding then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_2400';

                        coding.code as code where "code = 'unknown'" then 
                        {
                            code -> data.values as values, values.value = 'N\/A';
                        };

                        coding.code as code where "code != 'unknown'" then 
                        {
                            code -> data.values as values, values.value = code;
                        };
                    };
                };
            };    

            
            // Art der Resektion
            procedure -> tgt.data as data then
            {
                procedure.code as code, code.extension as resektion, resektion.extension as resektionsart, resektionsart where "url = 'resektionsart'" then
                {
                    resektionsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1176';
                        code -> data.values as values, values.value = code;
                    };

                    resektionsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'unknown'" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1176';
                        procedure -> data.values as values, values.value = 'N/A';
                    };
                };
            };
            
            // Resektionsstatus
            procedure -> tgt.data as data then
            {
                procedure.code as code, code.extension as resektion, resektion.extension as resektionsart, resektionsart where "url = 'resektionsstatus'" then
                {
                    resektionsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1353';
                        code -> data.values as values, values.value = code;
                    };
                };
            };
            

            // Lymphadenektomie - extension:lymphadenektomie
            procedure -> tgt.data as data then
            {
                procedure.extension as lymphadenektomie where "$this.extension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/lymphadenektomie'" then 
                {
                    lymphadenektomie.valueBoolean as value where "value = true" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1362';
                        procedure -> data.values as values, values.value = 'Ja';
                    };
                };
                
                procedure.extension as lymphadenektomie where "$this.extension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/lymphadenektomie'" then 
                {
                    lymphadenektomie.valueBoolean as value where "value = false" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1362';
                        procedure -> data.values as values, values.value = 'Nein';
                    };
                };
                
                procedure.extension as lymphadenektomie where "$this.extension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/lymphadenektomie'" then 
                {
                    lymphadenektomie.extension as extension where "$this.extension.valueCode = 'not-asked'" then
                    {
                        procedure -> data.blockindex = 1;
                        procedure -> data.groupindex = 0;
                        procedure -> data.itemid = 'id_1362';
                        procedure -> data.values as values, values.value = 'N\/A';
                    };
                };
            };
            
            // Bemerkungen
            procedure -> tgt.data as data then
            {
                procedure.note as note, note.text as text then
                {
                    procedure -> data.blockindex = 3;
                    procedure -> data.groupindex = 0;
                    procedure -> data.itemid = 'id_1472';
                    text -> data.values as values, values.value = text;
                };
            };
        };

        /* ------------------------------ Patient ---------------------------- */
        entry.resource as patient where "resource is Patient and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Patient/nNGM/patient'" then
        {
            // Todesdatum
            patient -> tgt.data as data then
            {
                patient.deceasedDateTime as deceasedDateTime then
                {
                    patient -> data.blockindex = 2;
                    patient -> data.groupindex = 0;
                    patient -> data.itemid = 'id_2401';
                    deceasedDateTime -> data.values as values, values.value = deceasedDateTime;
                };
            };
        };
    };
}