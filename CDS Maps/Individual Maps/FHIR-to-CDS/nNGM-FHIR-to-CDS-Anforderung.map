/// version = 0.1
/// title = "nNGM: nNGM_Mapping_AnforderungCTS - FHIR to CTS"


map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_AnforderungCDS" = nNGM_Mapping_AnforderungCDS

uses "http://hl7.org/fhir/StructureDefinition/Bundle" as source
uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as target

group TransformAnforderungCDS(source entry: BackboneElement, source gewebeBlut: string, target tgt: BackboneElement)
{
    entry where "%gewebeBlut = 'Gewebe'" then TransformAnforderungCDSGewebe(entry, tgt);
    entry where "%gewebeBlut = 'Blut'" then TransformAnforderungCDSBlut(entry, tgt);
}

group TransformAnforderungCDSGewebe(source entry: BackboneElement, target tgt: BackboneElement)
{
    /* ------------------------------ Specimen ---------------------------- */
    entry.resource as specimen where "resource is Specimen and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Specimen/nNGM'" then 
    {
        // Zu untersuchendes Material (Biopsie-ID)
        specimen -> tgt.data as data then
        {
            specimen.identifier as identifier then
            {   
                specimen -> data.blockindex = 0;
                specimen -> data.groupindex = 0;
                specimen -> data.itemid = 'id_2457';
                identifier.value as value-> data.values as values, values.value = value;
            };
        };     

        // Entnahmedatum 
        specimen -> tgt.data as data then
        {
            specimen.collection as collection then
            {
                collection.collectedDateTime as collectedDateTime then
                {
                    specimen -> data.blockindex = 1;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid = 'id_1551';
                    collectedDateTime -> data.values as values, values.value = collectedDateTime;
                };
            };
        };

        // Materialentnahme erfolgte
        specimen -> tgt.data as data then
        {
            specimen.extension as aufenthaltsart, aufenthaltsart.value as value where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart'" then
            {
                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'IMP'" then
                {
                    specimen -> data.blockindex = 1;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_1589';
                    code -> data.values as values, values.value = 'station\u00e4r';
                };

                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'AMB'" then
                {
                    specimen -> data.blockindex = 1;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_1589';
                    code -> data.values as values, values.value = 'ambulant';
                };

                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'not-applicable'" then
                {
                    specimen -> data.blockindex = 1;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_1589';
                    code -> data.values as values, values.value = 'N/A';
                };
            };
        };
    };

    /* ------------------------------ Observation ---------------------------- */
    entry.resource as observation where "resource is Observation and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/histo-vorbefund'" then 
    {
        //Histologie des vorbefunds
        observation -> tgt.data as data then
        {
            observation.valueCodeableConcept as vcc, vcc.coding as coding then
            {
                observation -> data.blockindex = 1;
                observation -> data.groupindex = 0;
                observation -> data.itemid = 'id_836';
                coding.code as code -> data.values as values, values.value = code;
            };
        };
    };

    /* ------------------------------ Organization ---------------------------- */
    entry.resource as organization where "resource is Organization and resource.meta.profile = 'http://clinicalsite.org/fhir/StructureDefinition/organizationalunit'" then 
    {
        organization -> tgt.data as data then
        {
            organization.name as name then 
            {
                organization -> data.blockindex = 4;
                organization -> data.groupindex = 0;
                organization -> data.itemid = 'id_1563';
                name -> data.values as values, values.value = name;
            };
        };
        
        organization -> tgt.data as data then
        {
            organization.address as address then 
            {
                address.text as text then 
                {
                    organization -> data.blockindex = 4;
                    organization -> data.groupindex = 0;
                    organization -> data.itemid = 'id_1562';
                    text -> data.values as values, values.value = text;
                };
            };
        };
    };

    /* ------------------------------ ServiceRequest ---------------------------- */
    entry.resource as servicerequest where "resource is ServiceRequest and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/tumorblock'" then 
    {
        // Pathologe
        servicerequest -> tgt.data as data then
        {
            servicerequest.performer as performer, performer.display as pathologe then 
            {
                servicerequest -> data.blockindex = 4;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid = 'id_1560';
                pathologe -> data.values as values, values.value = pathologe;
            };
        };

        // Diagnostik Typ
        servicerequest -> tgt.data as data then
        {
            servicerequest.category as category then
            {
                category.coding as coding where "coding.system = 'http://ncit.nci.nih.gov' and coding.code = 'C12801'" then
                {
                    servicerequest -> data.blockindex = 0;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_1368';
                    servicerequest -> data.values as values, values.value = 'Gewebediagnostik';
                };
            };
        };

        // GewÃ¼nschte diagnostik
        servicerequest -> tgt.data as data then
        {
            servicerequest.code as code, code.coding as coding where "coding.system = 'http://uk-koeln.de/fhir/CodeSystem/nngm/anforderung-code'" then                        
            {
                servicerequest -> data.blockindex = 3;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid = 'id_2473';
                coding.code as code -> data.values as values, values.value = code;
            };
        };

        // orderDetail -> 3, 0, 2474 
        servicerequest -> tgt.data as data then
        {
            // set only if "Einzeluntersuchung" selected
            servicerequest.code as code, code.coding as coding where "coding.code = 'Einzeluntersuchung'" then
            {
                servicerequest.orderDetail as orderDetail then
                {
                    servicerequest -> data.blockindex = 3;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2474';

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C128554'" then
                    {
                        coding -> data.values as values, values.value = 'PD-L1';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C38184'" then
                    {
                        coding -> data.values as values, values.value = 'ALK';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C148329'" then
                    {
                        coding -> data.values as values, values.value = 'MET1';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C18399'" then
                    {
                        coding -> data.values as values, values.value = 'ROS1';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C18363'" then
                    {
                        coding -> data.values as values, values.value = 'BRAF';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C17757'" then
                    {
                        coding -> data.values as values, values.value = 'EGFR';
                    };

                    orderDetail.coding as orderCoding where "$this.coding.code = 'C25785'" then
                    {
                        coding -> data.values as values, values.value = 'KRAS';
                    };
                };
            }; 
        };
            
        // Tumorblockanforderung
        servicerequest -> tgt.data as data then
        {
            servicerequest.doNotPerform as dnp where "doNotPerform = false" then
            {
                servicerequest -> data.blockindex = 4;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid  = 'id_2476';
                dnp -> data.values as values, values.value = 'no';
            };
        };

        servicerequest -> tgt.data as data then
        {
            servicerequest.doNotPerform as dnp where "doNotPerform = true" then
            {
                servicerequest -> data.blockindex = 4;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid  = 'id_2476';
                dnp -> data.values as values, values.value = 'yes';
            };
        };

        //Grund zum entfallen der Tumorblockanforderung
        servicerequest -> tgt.data as data then
        {
            servicerequest.extension as entfallen, entfallen.value as value where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/grundEntfallBlockAnforderung'" then 
            {
                entfallen.valueCodeableConcept as vcc where "$this.valueCodeableConcept.coding.code = 'bereitsUnterwegs'", vcc.coding as coding, coding.code as code then 
                {
                    code -> data.blockindex = 4;
                    code -> data.groupindex = 0;
                    code -> data.itemid = 'id_2490';
                    code -> data.values as values, values.value = 'Tumormaterial bereits unterwegs';
                };

                entfallen.valueCodeableConcept as vcc where "$this.valueCodeableConcept.coding.code = 'innerePathologie'", vcc.coding as coding, coding.code as code then 
                {
                    code -> data.blockindex = 4;
                    code -> data.groupindex = 0;
                    code -> data.itemid = 'id_2490';
                    code -> data.values as values, values.value = 'Antrag interne Pathologie';
                };
            };
        };

        // Tumorblock anforderung
        servicerequest -> tgt.data as data then
        {
            servicerequest.occurrenceDateTime as dt then 
            {
                    servicerequest -> data.blockindex = 4;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2477';
                    dt -> data.values as values, values.value = dt;
            };
        };

        // Wiederholungen
        servicerequest -> tgt.data as data then
        {
            servicerequest.extension as extension then  
            {
                extension.valueInteger as repeat where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/wiederholungAnforderung'" then 
                {
                    servicerequest -> data.blockindex = 4;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2478';
                    repeat -> data.values as values, values.value = repeat, values.unit = 'count';
                };
            };
        };

        // Storno
        servicerequest -> tgt.data as data then
        {
            servicerequest.extension as extension then 
            {
                extension.valueDateTime as valueDateTime where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/stornierungBlockAnforderung'" then
                {
                    valueDateTime -> data.blockindex = 4;
                    valueDateTime -> data.groupindex = 0;
                    valueDateTime -> data.itemid = 'id_2479';
                    valueDateTime -> data.values as values, values.value = valueDateTime;
                };
            };
        };

        // Versandmaterial
        servicerequest -> tgt.data as data then
        {
            servicerequest.extension as extension then 
            {
                extension.valueDateTime as valueDateTime where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/versandDatumBlockAnforderung'" then
                {
                    valueDateTime -> data.blockindex = 4;
                    valueDateTime -> data.groupindex = 0;
                    valueDateTime -> data.itemid = 'id_2480';
                    valueDateTime -> data.values as values, values.value = valueDateTime;
                };
            };
        };          
    };
}

group TransformAnforderungCDSBlut(source entry: BackboneElement, target tgt: BackboneElement)
{
    /* ------------------------------ Specimen ---------------------------- */
    entry.resource as specimen where "resource is Specimen and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Specimen/nNGM'" then 
    { 
        // Zu untersuchendes Material (Biopsie-ID)
        specimen -> tgt.data as data then
        {
            specimen.identifier as identifier then
            {   
                specimen -> data.blockindex = 0;
                specimen -> data.groupindex = 0;
                specimen -> data.itemid = 'id_2457';
                identifier.value as value-> data.values as values, values.value = value;
            };
        }; 

        // Entnahmedatum 
        specimen -> tgt.data as data then
        {
            specimen.collection as collection then
            {
                collection.collectedDateTime as collectedDateTime then
                {
                    specimen -> data.blockindex = 2;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid = 'id_2316';
                    collectedDateTime -> data.values as values, values.value = collectedDateTime;
                };
            };
        };

        // Blutentnahme erfolgte
        specimen -> tgt.data as data then
        {
            specimen.extension as aufenthaltsart, aufenthaltsart.value as value where "$this.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart'" then
            {
                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'IMP'" then
                {
                    specimen -> data.blockindex = 2;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_2320';
                    code -> data.values as values, values.value = 'station\u00e4r';
                };

                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'AMB'" then
                {
                    specimen -> data.blockindex = 2;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_2320';
                    code -> data.values as values, values.value = 'ambulant';
                };

                aufenthaltsart.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as code where "code = 'not-applicable'" then
                {
                    specimen -> data.blockindex = 2;
                    specimen -> data.groupindex = 0;
                    specimen -> data.itemid  = 'id_2320';
                    code -> data.values as values, values.value = 'N/A';
                };
            };
        };
    };
    
    /* ------------------------------ Observation ---------------------------- */
    entry.resource as observation where "resource is Observation and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/histo-vorbefund'" then 
    {
        // Histologie des vorbefunds (Blutdiagnostik)
        observation -> tgt.data as data then
        {
            observation.valueCodeableConcept as vcc then
            {
                observation -> data.blockindex = 2;
                observation -> data.groupindex = 0;
                observation -> data.itemid = 'id_2319';

                vcc.coding as coding where "coding.code = 'C3812'" then 
                {
                    coding.code as code -> data.values as values, values.value = 'CUP';
                };
                
                vcc.coding as coding where "coding.code != 'C3812'" then 
                {
                    coding.code as code -> data.values as values, values.value = code;
                };
            };
        };
    };

    /* ------------------------------ ServiceRequest ---------------------------- */
    entry.resource as servicerequest where "resource is ServiceRequest and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/testung'" then 
    {
        // Diagnostik Typ
        servicerequest -> tgt.data as data then
        {
            servicerequest.category as category then
            {
                category.coding as coding where "coding.system = 'http://ncit.nci.nih.gov' and coding.code = 'C12434'" then
                {
                    servicerequest -> data.blockindex = 0;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_1368';
                    servicerequest -> data.values as values, values.value = 'Blutdiagnostik (Liquid Biopsy)';
                };
            };
        };

        // Liquid Biopsy
        servicerequest -> tgt.data as data then
        {
            servicerequest.orderDetail as orderDetail then 
            {
                servicerequest -> data.blockindex = 3;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid = 'id_2313';

                orderDetail.coding as coding where "$this.coding.code = 'C16885'", coding.code as code then 
                {
                    code -> data.values as values, values.value = 'Prim\u00e4rmutation';
                };

                orderDetail.coding as coding where "$this.coding.code = 'C19391'", coding.code as code then 
                {
                    code -> data.values as values, values.value = 'Resistenztestung';
                };
            };
        };

        // GrundFehlendeBiopsie 
        servicerequest -> tgt.data as data then
        {
            servicerequest.extension as extension, extension.url as url where "url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/GrundFehlendeBiopsy'" then 
            {                
                servicerequest -> data.blockindex = 3;
                servicerequest -> data.groupindex = 0;
                servicerequest -> data.itemid = 'id_2314';

                extension.valueCodeableConcept as valueCodeableConcept where "$this.valueCodeableConcept.coding.code = 'FT'" then 
                {
                    valueCodeableConcept -> data.values as values, values.value = 'Fehlende Tumormanifestation, die einer Biopsie zug\u00e4nglich w\u00e4re';
                };

                extension.valueCodeableConcept as valueCodeableConcept where "$this.valueCodeableConcept.coding.code = 'RA'" then 
                {
                    valueCodeableConcept -> data.values as values, values.value = 'Reduzierter Allgemeinzustand';
                };

                extension.valueCodeableConcept as valueCodeableConcept where "$this.valueCodeableConcept.coding.code = 'UR'" then 
                {
                    valueCodeableConcept -> data.values as values, values.value = 'Unzumutbares Risikos einer Biopsie bzw. zu erwartende Komplikationen';
                };

                extension.valueCodeableConcept as valueCodeableConcept where "$this.valueCodeableConcept.coding.code = 'AP'" then 
                {
                    valueCodeableConcept -> data.values as values, values.value = 'Ablehnung seitens Patient(in)';
                };
            };
        };
    }; 
}