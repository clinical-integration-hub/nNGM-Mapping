/// version = 0.1
/// title = "StrahlentherapieMap"

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_StrahlentherapieFHIR" = nNGM_Mapping_StrahlentherapieFHIR

uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as source
uses "http://hl7.org/fhir/StructureDefinition/Procedure" as target
uses "http://hl7.org/fhir/StructureDefinition/EpisodeOfCare" as target
uses "http://hl7.org/fhir/StructureDefinition/Patient" as target


group TransformBundleStrahlentherapie(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    operations then CreateProcedureStrahlentherapie(operations, bundle, composition, section);
    operations then CreateEpisodeOfCareStrahlentherapie(operations, bundle, composition, section);
}

/* ------------------------------ Check whether Procedure needs to be created ---------------------------- */
group CreateProcedureStrahlentherapie(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    let resourceIsCreated = create('Boolean');
    operations then SetBooleanToFalse(operations, resourceIsCreated);

    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid ='id_1185'
                                                        or blockindex = 0 and groupindex = 0 and itemid ='id_1186'
                                                        or blockindex = 0 and groupindex = 0 and itemid ='id_1182'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1181'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1183'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1196'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1198'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1199'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1363'
                                                        or blockindex = 1 and groupindex = 0 and itemid ='id_1364'
                                                        or blockindex = 2 and groupindex = 0 and itemid ='id_1211'" then
    {
        // if resource is not created yet: call transform
        operations where "%resourceIsCreated.valueBoolean = false" -> bundle.entry as entry, entry.resource = create('Procedure') as procedure then 
        {
            operations then TransformProcedureStrahlentherapie(operations, procedure, composition, section);
            operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(procedure, '\'Procedure/\' + $this.id');
            operations then SetBooleanToTrue(operations, resourceIsCreated);
        }; 
    };
}

/* ------------------------------ Procedure ---------------------------- */
group TransformProcedureStrahlentherapie(source operations: BackboneElement, target tgt: Procedure, target composition: Composition, target section: BackboneElement)
{
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Procedure/nNGM/Strahlentherapie';

    // Category
    operations -> tgt.category as category then
    {
        operations -> category.coding = create ('Coding') as coding, coding.system = 'http://uk-koeln.de/fhir/CodeSystem/cancer-base/procedure-category',
                                                                    coding.code = 'radiotherapy';                                                    
    };

    // Code
    operations -> tgt.code = cc('http://ncit.nci.nih.gov', 'C15313');

    // Subject
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_nngm_strahlentherapie'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as procedure, procedure.reference = evaluate(value, '\'Procedure/\' + $this');
        };

        //------------[DURCHFÜHRUNG]------------
        //Therapiebeginn
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid ='id_1185'" then
        {
            values.value as start -> tgt.performed = create('Period') as period collate, period.start = dateOp(start, 'date');
        };

        //Therapieende
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid ='id_1186'" then
        {
            values.value as end -> tgt.performed = create('Period') as period collate, period.end = dateOp(end, 'date');
        };

        //Durchführende Einrichtung
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid ='id_1182'" then
        {
            values.value as value -> tgt.performer as performer, 
                                    performer.actor = create('Reference') as actor,
                                                        actor.display = value;
        };

        //------------[THERAPIEMERKMALE]------------
        //Bestrahlungsziel
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1181'" then
        {
            values.value as value where "value =  'N/A'" then 
            {
                value -> tgt.bodySite = cc('http://terminology.hl7.org/CodeSystem/data-absent-reason','not-applicable','Not Applicable');
            };
            values.value as value where "value !=  'N/A'" then 
            {
                value -> tgt.bodySite = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/Strahlentherapie_Bestrahlungsziel',value,value);
            };
            
        };

        //Therapeutische Intention
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1183'" then
        {
            values.value as value where "value =  'N/A'" then 
            {
               value -> tgt.reasonCode = cc('http://terminology.hl7.org/CodeSystem/data-absent-reason','unknown','Unknown');
            };
            values.value as value where "value !=  'N/A'" then 
            {
                value -> tgt.reasonCode = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/intention-therapie',value,value);
            };
        };

        //Art der Bestrahlung
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1196'" then
        {
             values.value as value where "value =  'N/A'" then 
            {
                value -> tgt.code as code,
                        code.extension as artDerBestrahlung,
                        artDerBestrahlung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/art-der-bestrahlung',
                        artDerBestrahlung.valueCodeableConcept = cc('http://terminology.hl7.org/CodeSystem/data-absent-reason','unknown','Unknown');
            };
            values.value as value where "value !=  'N/A'" then 
            {
                 value -> tgt.code as code,
                        code.extension as artDerBestrahlung,
                        artDerBestrahlung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/art-der-bestrahlung',
                        artDerBestrahlung.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nNGM/StrahlentherapieArtDerBestrahlung',value,value);
            };           
        };

        //Dosis
        //Einzeldosis (Gy)
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1198'" then
        {
            values -> tgt.extension as dosis, 
                        dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                        dosis.extension as einzeldosis then
                        {
                            values -> einzeldosis.url = 'einzeldosis';
                            values -> einzeldosis.valueQuantity = create('Quantity') as quantity then 
                                {
                                    values.value as value -> quantity.value = value;
                                    values.value as value -> quantity.system = 'http://unitsofmeasure.org';
                                    values.unit as unit   -> quantity.code = unit;
                                };                                                  
                        };
        
        };

        //Gesamtdosis (Gy)
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1199'" then
        {
            values -> tgt.extension as dosis collate, 
                        dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                        dosis.extension as gesamtdosis then
                        {
                            values -> gesamtdosis.url = 'gesamtdosis';
                            values -> gesamtdosis.valueQuantity = create('Quantity') as quantity then 
                                {
                                    values.value as value -> quantity.value = value;
                                    values.value as value -> quantity.system = 'http://unitsofmeasure.org';
                                    values.unit as unit   -> quantity.code = unit;
                                };                                                  
                        };
        };

        //Boost
        //Boost durchgeführt
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1363'" then
        {
            values.value as value where "value =  'ja'" then 
            {
                value -> tgt.extension as dosis collate,
                        dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                        dosis.extension as boost then
                        {
                            value -> boost.url = 'boost';
                            value -> boost.valueBoolean = true;
                        };
            };
            values.value as value where "value =  'nein'" then 
            {
                value -> tgt.extension as dosis collate,
                        dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                        dosis.extension as boost then
                        {
                            value -> boost.url = 'boost';
                            value -> boost.valueBoolean = false;
                        };
            };
            values.value as value where "value =  'ja, Boostdosis in Gesamtdosis enthalten'" then 
            {
                value -> tgt.extension as dosis collate,
                        dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                        dosis.extension as boostdosis then
                        {
                            value -> boostdosis.url = 'boostdosisInGesamtdosis';
                            value -> boostdosis.valueBoolean = true;
                        };
            };
        };

        //Boostdosis (Gy)
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1364'" then
        {
            values -> tgt.extension as dosis collate, 
                    dosis.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/strahlentherapie-dosis',
                    dosis.extension as boostdosis then
                    {
                        values -> boostdosis.url = 'boostdosis';
                        values -> boostdosis.valueQuantity = create('Quantity') as quantity then 
                            {
                                values.value as value -> quantity.value = value;
                                values.value as value -> quantity.system = 'http://unitsofmeasure.org';
                                values.unit as unit   -> quantity.code = unit;
                            };                                                  
                    };
        };

        //------------[Ende der Therapie]------------
        data where "$this.where(blockindex = 2 and groupindex = 0 and itemid ='id_1211').exists()"
            -> tgt.statusReason = create('CodeableConcept') as cc then 
        {
            data.values as values, values.value as value where "value != 'N/A'" -> cc.coding = c('https://www.uk-koeln.de/nngm/CodeSystem/ST_Ende_Grund', value);
            data.values as values, values.value as value where "value = 'N/A'" -> cc.coding = c('http://terminology.hl7.org/CodeSystem/data-absent-reason', 'not-applicable');

            data.values as values, values.value as value where "value = 'Regul\u00e4res Ende' or value = 'Reguläres Ende'" -> tgt.status = 'completed';
            data.values as values, values.value as value where "value = 'Pause wegen Nebenwirkungen'" -> tgt.status = 'on-hold';
            data.values as values, values.value as value where "value = 'Ablehnung durch den Patienten'" -> tgt.status = 'not-done';
            data.values as values, values.value as value where "value = 'Abbruch wegen Nebenwirkungen' or value = 'Abbruch wegen Progress' or value = 'Patient verstorben'" -> tgt.status = 'stopped';
            data.values as values, values.value as value where "value = 'Sonstiges' or value = 'N/A'" -> tgt.status = 'unknown';
        };

        //Bemerkungen
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid ='id_1190'" then
        {
            values -> tgt.note = create('Annotation') as annotation then 
            {
                values.value as value -> annotation.text = value;
            };
        };
    };
}

/* ------------------------------ Check whether EpisodeOfCare needs to be created ---------------------------- */
group CreateEpisodeOfCareStrahlentherapie(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    operations.data as data, data.values as values where "blockindex = 2 and groupindex = 0 and itemid ='id_1543'" then
    {
        operations -> bundle.entry as entry, entry.resource = create('EpisodeOfCare') as episodeOfCare then 
        {
            operations then TransformEpisodeOfCareStrahlentherapie(operations, episodeOfCare, composition, section);
            operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(episodeOfCare, '\'EpisodeOfCare/\' + $this.id');
        };
    };
}

/* ------------------------------ EpisodeOfCare ---------------------------- */
group TransformEpisodeOfCareStrahlentherapie(source operations: BackboneElement, target tgt: EpisodeOfCare, target composition: Composition, target section: BackboneElement)
{
    // Profile url
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/EpisodeOfCare/nNGM';

    // Identifier.system and Identifier.value: items required on simplifier
    operations ->  tgt.identifier = create('Identifier') as nngmFallnummer,
                    nngmFallnummer.system = 'http://uk-koeln.de/NamingSystem/nNGM/fallnummer',
                    nngmFallnummer.value as value,
                    value.extension as dataAbsentReason,
                    dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                    dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Status: item required on simplifier
    operations -> tgt.status = 'active';

    // Patient
    operations -> tgt.patient = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_nngm_dateOfProgress3'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as episodeOfCare, episodeOfCare.reference = evaluate(value, '\'EpisodeOfCare/\' + $this');
        };

        //Datum Progress
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid ='id_1543'" then
        {
            values.value as value ->    tgt.extension as datumProgress,
                                        datumProgress.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/datumProgress',
                                        datumProgress.valueDate = dateOp(value, 'date');
        };
    };
}