/// version = 0.1
/// title = "nNGM_Mapping_Anforderung"

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_Anforderung" = nNGM_Mapping_Anforderung

uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as source
uses "http://hl7.org/fhir/StructureDefinition/ServiceRequest" as target
uses "http://hl7.org/fhir/StructureDefinition/Specimen" as target
uses "http://hl7.org/fhir/StructureDefinition/Observation" as target
uses "http://hl7.org/fhir/StructureDefinition/Organization" as target

/*
    TODO
    - creation of specimen is currently commented out (with uuid_specimen_nngm_blut / _block)
    - reference to created specimen is currently commented out (in the other transform-groups)
    - deal with biopsy-ID
*/

group TransformBundleAnforderung(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target biopsieSection: BackboneElement)
{
    operations -> biopsieSection.section = create('BackboneElement') as section, section.title = 'anforderung', section.code = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/sections', 'anforderung') then
    {
        operations then CreateServiceRequestAnforderung(operations, bundle, composition, section);
        // operations then CreateSpecimenAnforderung(operations, bundle, composition, section);
        operations then CreateObservationAnforderung(operations, bundle, composition, section);
        operations then CreateOrganizationAnforderung(operations, bundle, composition, section);
    };
}

/* ------------------------------ Check whether ServiceRequest needs to be created ---------------------------- */
group CreateServiceRequestAnforderung(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    // Check whether observation should be created
    let resourceShouldBeCreated = create('Boolean');

    // Resource elements displayed just when "Gewebediagnostik" was selected
    operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Gewebediagnostik'
                                                        or blockindex = 3 and groupindex = 0 and itemid = 'id_2473'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2476'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2490'
                                                        or blockindex = 3 and groupindex = 0 and itemid = 'id_2474'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2477'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2478'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2479'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_2480'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_1560'" then
    {
        operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
    };

    // Create resource if bool set to true
    operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('ServiceRequest') as serviceRequest then 
    {
        operations then TransformGewebeDiagnostikServiceRequest(operations, serviceRequest, composition, section);
        operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(serviceRequest, '\'ServiceRequest/\' + $this.id');
    };


    // Resource elements displayed just when "Blutdiagnostik" was selected
    operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Blutdiagnostik (Liquid Biopsy)'
                                                        or blockindex = 3 and groupindex = 0 and itemid = 'id_2313'
                                                        or blockindex = 3 and groupindex = 0 and itemid = 'id_2314'" then
    {
        operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
    };

    // Create resource if bool set to true
    operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('ServiceRequest') as serviceRequest then 
    {
        operations then TransformBlutDiagnostikServiceRequest(operations, serviceRequest, composition, section);
        operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(serviceRequest, '\'ServiceRequest/\' + $this.id');
    };
}

/* ------------------------------ ServiceRequest - GewebeDiagnostik ---------------------------- */
group TransformGewebeDiagnostikServiceRequest(source operations: BackboneElement, target tgt: ServiceRequest, target composition: Composition, target section: BackboneElement)
{
    // profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/tumorblock';
    
    // subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // status, which is obligatory element on Simplifier, with an absent extension
    operations -> tgt.status = cast('active', 'FHIR.code');

    // intent, which is obligatory element on Simplifier
    operations -> tgt.intent = cast('proposal', 'FHIR.code');

    // code, which is obligatory element on Simplifier, using system and code values
    operations -> tgt.code = cc('http://ncit.nci.nih.gov', 'C18202');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'uuid_nngm_tumorblock'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as serviceRequest, serviceRequest.reference = evaluate(value, '\'ServiceRequest/\' + $this');
        };

        // category
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368'" then
        {
            values.value as value where "$this.value = 'Gewebediagnostik'" then
            {
                value -> tgt.category = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-kategorie', 'C12801', value);
            };
        };
        
        // code
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_2473'" then
        {
            values.value as value -> tgt.code = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-code', value);
        };

        // orderDetail
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_2474'" then
        {
            values.value as value where "$this.value = 'PD-L1'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C128554', value);
            };

            values.value as value where "$this.value = 'ALK'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C38184', value);
            };
            
            values.value as value where "$this.value = 'MET1'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C148329', value);
            };

            values.value as value where "$this.value = 'ROS1'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C18399', value);
            };

            values.value as value where "$this.value = 'BRAF'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C18363', value);
            };

            values.value as value where "$this.value = 'EGFR'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C17757', value);
            };

            values.value as value where "$this.value = 'KRAS'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C25785', value);
            };
        };

        // doNotPerform
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2476'" then 
        {
            values.value where "value = 'no'" then 
            {
                values.value as value -> tgt.doNotPerform = cast(false, 'FHIR.boolean');
            };

            values.value where "value = 'yes'" then
            {
                values.value as value -> tgt.doNotPerform = cast(true, 'FHIR.boolean');
            };
        };

        // extension:grundEntfallBlockAnforderung
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2490'" then
        {
            values.value where "%tgt.doNotPerform = false" then
            {
                values.value where "$this.value = 'Tumormaterial bereits unterwegs'" then
                {
                    values.value as value -> tgt.extension as tgtExtension,
                                            tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/grundEntfallBlockAnforderung',
                                            tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/grundEntfallBlockAnforderung', 'bereitsUnterwegs');
                };

                values.value where "$this.value = 'Antrag interne Pathologie'" then
                {
                    values.value as value -> tgt.extension as tgtExtension,
                                            tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/grundEntfallBlockAnforderung',
                                            tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/grundEntfallBlockAnforderung', 'innerePathologie');
                };
            };
        };

        // occurrenceDateTime
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2477'" then
        {
            values.value as value ->  tgt.occurrenceDateTime =  dateOp(value, 'date');
        };

        // extension:wiederholungAnforderung.valueInteger
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2478'" then
        {
            values.value where "%tgt.doNotPerform = true" then
            {
                values.value as value -> tgt.extension as wiederholung, 
                                        wiederholung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/wiederholung', 
                                        wiederholung.valueInteger = cast(value, 'FHIR.integer');
            };
        };

        // extension:stornierungBlockAnforderung,valueDateTIme
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2479'" then
        {
            values.value where "%tgt.doNotPerform = true" then
            {
                values.value as value -> tgt.extension as storno, 
                                        storno.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/stornierungBlockAnforderung', 
                                        storno.valueDateTime = dateOp(value, 'date');
            };
        };

        // extension:versandDatum.valueDateTime
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2480'" then
        {
            values.value where "%tgt.doNotPerform = true" then
            {
                values.value as value -> tgt.extension as versandmaterial, 
                                  versandmaterial.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/versandDatumBlockAnforderung', 
                                  versandmaterial.valueDateTime = dateOp(value, 'date');
            };
        };

        // performer.reference.display
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_1560'" then
        {
            values.value as value -> tgt.performer as performer, performer.display = value, performer.reference = value;
        };

        // Referenzen
        // Organization
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'uuid_organizationalunit_pathology'" then
        {
            values.value as value -> tgt.requester = create('Reference') as requester, requester.reference = evaluate(value, '\'Organization/\' + $this');
        };

        // Observation
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_nngm_histoVorbefund_block'" then
        {
            values.value as value -> tgt.reasonReference = create('Reference') as observation, observation.reference = evaluate(value, '\'Observation/\' + $this');
        };

        // Specimen
        // data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_specimen_nngm_block'" then
        // {
        //     values.value as value -> tgt.specimen = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        // };
    };
}

/* ------------------------------ ServiceRequest - BlutDiagnostik ---------------------------- */
group TransformBlutDiagnostikServiceRequest(source operations: BackboneElement, target tgt: ServiceRequest, target composition: Composition, target section: BackboneElement)
{
    // profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/testung';
    
    // subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // status, which is obligatory element on Simplifier, with an absent extension
    operations -> tgt.status as status, status.extension as dataAbsentReason, 
                                                     dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                                                     dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // intent, which is obligatory element on Simplifier
    operations -> tgt.intent = cast('proposal', 'FHIR.code');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'uuid_nngm_testung'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as serviceRequest, serviceRequest.reference = evaluate(value, '\'ServiceRequest/\' + $this');
        };

        // category
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid ='id_1368'" then
        {
            values.value as value where "$this.value = 'Blutdiagnostik (Liquid Biopsy)'" then
            {
                value -> tgt.category = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-kategorie', 'C12434', value);
            };
        };

        // orderDetail
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_2313'" then
        {
            values.value as value where "$this.value = 'Primärmutation' or $this.value = 'Prim\u00e4rmutation'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C16885', value);
            };

            values.value as value where "$this.value = 'Resistenztestung'" then
            {
                value -> tgt.orderDetail = cc('http://uk-koeln.de/fhir/ValueSet/nngm/anforderung-orderDetails', 'C19391', value);
            };
        };

        // .extension:grundFehlend.valueCodeableConcept
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_2314'" then
        {
            values.value where "$this.value = 'Fehlende Tumormanifestation, die einer Biopsie zugänglich wäre'" then
            {
                values.value as value -> tgt.extension as tgtExtension,
                                        tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/GrundFehlendeBiopsy',
                                        tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nNGM/grundfehlendeBiopsie', 'FT', value);
            };

            values.value where "$this.value = 'Reduzierter Allgemeinzustand'" then
            {
                values.value as value -> tgt.extension as tgtExtension,
                                        tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/GrundFehlendeBiopsy',
                                        tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nNGM/grundfehlendeBiopsie', 'RA', value);
            };

            values.value where "$this.value = 'Unzumutbares Risikos einer Biopsie bzw. zu erwartende Komplikationen'" then
            {
                values.value as value -> tgt.extension as tgtExtension,
                                        tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/GrundFehlendeBiopsy',
                                        tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nNGM/grundfehlendeBiopsie', 'UR', value);
            };

            values.value where "$this.value = 'Ablehnung seitens Patient(in)'" then
            {
                values.value as value -> tgt.extension as tgtExtension,
                                        tgtExtension.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/GrundFehlendeBiopsy',
                                        tgtExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nNGM/grundfehlendeBiopsie', 'AP', value);
            };
        };

        // Referenzen
        // Organization
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'uuid_organizationalunit_pathology'" then
        {
            values.value as value -> tgt.requester = create('Reference') as requester, requester.reference = evaluate(value, '\'Organization/\' + $this');
        };

        // Observation
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_nngm_histoVorbefund_blut'" then
        {
            values.value as value -> tgt.reasonReference = create('Reference') as observation, observation.reference = evaluate(value, '\'Observation/\' + $this');
        };

        // Specimen
        // data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_specimen_nngm_blut'" then
        // {
        //     values.value as value -> tgt.specimen = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        // };
    };
}

/* ------------------------------ Check whether Specimen needs to be created ---------------------------- */
group CreateSpecimenAnforderung(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    operations.data as data then
    {
        // Check whether observation should be created
        let resourceShouldBeCreated = create('Boolean');

        // Resource elements displayed just when "Gewebediagnostik" was selected
        operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Gewebediagnostik'
                                    or blockindex = 1 and groupindex = 0 and itemid ='id_1551'
                                    or blockindex = 1 and groupindex = 0 and itemid = 'id_1589'
                                    or blockindex = 0 and groupindex = 0 and itemid = 'id_2457'" then
        {
            operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
        };

        // Create resource if bool set to true
        operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('Specimen') as specimen then 
        {
            operations then TransformGewebeDiagnostikSpecimen(operations, specimen, composition, section);
            operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(specimen, '\'Specimen/\' + $this.id');
        };


        // Resource elements displayed just when "Blutdiagnostik" was selected
        operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Blutdiagnostik (Liquid Biopsy)'
                                    or blockindex = 2 and groupindex = 0 and itemid ='id_2316'
                                    or blockindex = 2 and groupindex = 0 and itemid = 'id_2320'
                                    or blockindex = 0 and groupindex = 0 and itemid = 'id_2457'" then
        {
            operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
        };

        // Create resource if bool set to true
        operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('Specimen') as specimen then 
        {
            operations then TransformBlutDiagnostikSpecimen(operations, specimen, composition, section);
            operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(specimen, '\'Specimen/\' + $this.id');
        };
    };
}

/* ------------------------------ Specimen - GewebeDiagnostik ---------------------------- */
group TransformGewebeDiagnostikSpecimen(source operations: BackboneElement, target tgt: Specimen, target composition: Composition, target section: BackboneElement)
{
    //profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Specimen/nNGM';

    //subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // Mapped type, which is obligatory element on Simplifier, with no fixed value
    operations ->  tgt.type as type,
                    type.coding as coding,
                    coding.system = 'http://snomed.info/sct',
                    coding.version = '900000000000207008',
                    coding.code as code,
                    code.extension as dataAbsent,
                    dataAbsent.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                    dataAbsent.valueCode = cast('not-asked', 'FHIR.code');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_specimen_nngm_block'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        };
        
        // collection.collectedDateTime
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid ='id_1551'" then
        {
            values.value as value -> tgt.collection as collection, collection.collectedDateTime = dateOp(value, 'date');
        };

        // extension:entnahmeKontext.valueCodeableConcept
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1589'" then
        {
            values.value where "$this.value = 'stationär' or $this.value = 'station\u00e4r'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/aufenthaltsart', 'IMP', 'inpatient encounter');
            };

            values.value where "$this.value = 'ambulant'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/aufenthaltsart', 'AMB', 'ambulatory');
            };
            
            values.value where "$this.value = 'N\/A' or $this.value = 'N/A'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://terminology.hl7.org/CodeSystem/v3-NullFlavor', 'UNK', 'unknown');
            };
        };

        // Referenzen
        // ServiceRequest
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'uuid_nngm_tumorblock'" then
        {
            values.value as value -> tgt.request = create('Reference') as request, request.reference = evaluate(value, '\'ServiceRequest/\' + $this');
        };

        // //Identifier:biopsieID.value
        // data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_2457'" then
        // {
        //     values.value as value ->  tgt.identifier = create('Identifier') as identifier, 
        //                               identifier.system = 'http://uk-koeln.de/NamingSystem/nNGM/biopsienummer', 
        //                               identifier.value = value;
        // };
    };
}

/* ------------------------------ Specimen - BlutDiagnostik ---------------------------- */
group TransformBlutDiagnostikSpecimen(source operations: BackboneElement, target tgt: Specimen, target composition: Composition, target section: BackboneElement)
{
    // profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Specimen/nNGM';

    // subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // type, which is obligatory element on Simplifier, with no fixed value
    operations ->  tgt.type as type,
                    type.coding as coding,
                    coding.system = 'http://snomed.info/sct',
                    coding.version = '900000000000207008',
                    coding.code as code,
                    code.extension as dataAbsent,
                    dataAbsent.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                    dataAbsent.valueCode = cast('not-asked', 'FHIR.code');

    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_specimen_nngm_blut'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        };

        // collection.collectedDateTime
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid ='id_2316'" then
        {
            values.value as value -> tgt.collection as collection, collection.collectedDateTime = dateOp(value, 'date');
        };

        // extension:entnahmeKontext.valueCodeableConcept
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2320'" then
        {
            values.value where "$this.value = 'stationär' or $this.value = 'station\u00e4r'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/aufenthaltsart', 'IMP', 'inpatient encounter');
            };

            values.value where "$this.value = 'ambulant'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/aufenthaltsart', 'AMB', 'ambulatory');
            };
            
            values.value where "$this.value = 'N/A' or $this.value = 'N\/A'" then 
            {
                values.value as za -> tgt.extension as entnahmeKontext, 
                                    entnahmeKontext.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/aufenthaltsart', 
                                    entnahmeKontext.valueCodeableConcept = cc('http://terminology.hl7.org/CodeSystem/v3-NullFlavor', 'UNK', 'unknown');
            };
        };

        // Referenzen
        // ServiceRequest
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'uuid_nngm_testung'" then
        {
            values.value as value -> tgt.request = create('Reference') as request, request.reference = evaluate(value, '\'ServiceRequest/\' + $this');
        };

        // //Identifier:biopsieID.value
        // data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_2457'" then
        // {
        //     values.value as value ->  tgt.identifier = create('Identifier') as identifier, 
        //                               identifier.system = 'http://uk-koeln.de/NamingSystem/nNGM/biopsienummer', 
        //                               identifier.value = value;
        // };
    };
}

/* ------------------------------ Check whether Observation needs to be created ---------------------------- */
group CreateObservationAnforderung(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    // Check whether observation should be created
    let resourceShouldBeCreated = create('Boolean');

    // Resource elements displayed just when "Gewebediagnostik" was selected
    operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Gewebediagnostik'
                                                        or blockindex = 1 and groupindex = 0 and itemid = 'id_836'" then
    {
        operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
    };

    // Create resource if bool set to true
    operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('Observation') as observation then 
    {
        operations then TransformObservationGewebediagnostikAnforderung(operations, observation, composition, section);
        operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(observation, '\'Observation/\' + $this.id');
    };


    // Resource elements displayed just when "Blutdiagnostik" was selected
    operations then SetBooleanToFalse(operations, resourceShouldBeCreated);
    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Blutdiagnostik (Liquid Biopsy)'
                                                        or blockindex = 2 and groupindex = 0 and itemid = 'id_2319'" then
    {
        operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
    };

    // Create resource if bool set to true
    operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('Observation') as observation then 
    {
        operations then TransformObservationBlutDiagnostikAnforderung(operations, observation, composition, section);
        operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(observation, '\'Observation/\' + $this.id');
    };
}

/* ------------------------------ Observation - GewebeDiagnostik ---------------------------- */
group TransformObservationGewebediagnostikAnforderung(source operations: BackboneElement, target tgt: Observation, target composition: Composition, target section: BackboneElement)
{
    // profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/histo-vorbefund';
    
    // subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // Obligatory fields
    operations -> tgt.status = cast('final', 'FHIR.code');
    operations -> tgt.category = cc('http://hl7.org/fhir/observation-category', 'laboratory');
    operations -> tgt.code = cc('http://ncit.nci.nih.gov', 'C19165');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_nngm_histoVorbefund_block'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as observation, observation.reference = evaluate(value, '\'Observation/\' + $this');
        };

        // Histologie des vorbefunds - valueCodeableConcept
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_836'" then 
        {
            values.value as valuecc ->  tgt.valueCodeableConcept as vcc, vcc.coding as coding collate,
                         coding.system = 'http://uk-koeln.de/fhir/ValueSet/nngm/histologie-klassifikation',
                         coding.code = evaluate(valuecc, '$this.split(\'\\t\').first()'),
                         coding.display = evaluate(valuecc, '$this.split(\'\\t\').last()');
        };

        // Referenzen
        // Specimen
        // data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'uuid_specimen_nngm_block'" then
        // {
        //     values.value as value -> tgt.specimen = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        // };
    };
}

/* ------------------------------ Observation - BlutDiagnostik ---------------------------- */
group TransformObservationBlutDiagnostikAnforderung(source operations: BackboneElement, target tgt: Observation, target composition: Composition, target section: BackboneElement)
{
    // profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/histo-vorbefund';
    
    // subject, which is obligatory element on Simplifier, with a Patient
    operations -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(composition, '\'Patient/\' + $this.subject.reference');

    // Obligatory fields
    operations -> tgt.status = cast('final', 'FHIR.code');
    operations -> tgt.category = cc('http://hl7.org/fhir/observation-category', 'laboratory');
    operations -> tgt.code = cc('http://ncit.nci.nih.gov', 'C19165');

    // Access data
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_specimen_nngm_blut'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as observation, observation.reference = evaluate(value, '\'Observation/\' + $this');
        };

        // Histologie des vorbefunds - valueCodeableConcept
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2319'" then 
        {
            values.value as valuecc ->  tgt.valueCodeableConcept as vcc, vcc.coding as coding collate,
                         coding.system = 'http://uk-koeln.de/fhir/ValueSet/nngm/histologie-klassifikation',
                         coding.code = evaluate(valuecc, '$this.split(\'\\t\').first()'),
                         coding.display = evaluate(valuecc, '$this.split(\'\\t\').last()');
        };

        // Referenzen
        // Specimen
        // data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'uuid_specimen_nngm_blut'" then
        // {
        //     values.value as value -> tgt.specimen = create('Reference') as specimen, specimen.reference = evaluate(value, '\'Specimen/\' + $this');
        // };
    };
}

/* ------------------------------ Check whether Organization needs to be created ---------------------------- */
group CreateOrganizationAnforderung(source operations: BackboneElement, target bundle: Bundle, target composition: Composition, target section: BackboneElement)
{
    // Check whether observation should be created
    let resourceShouldBeCreated = create('Boolean');
    operations then SetBooleanToFalse(operations, resourceShouldBeCreated);

    // Resource elements displayed just when "Gewebediagnostik" was selected
    operations.data as data, data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1368' and values.value = 'Gewebediagnostik'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_1563'
                                                        or blockindex = 4 and groupindex = 0 and itemid = 'id_1562'" then
    {
        operations then SetBooleanToTrue(operations, resourceShouldBeCreated);
    };

    // Create resource if bool set to true
    operations where "%resourceShouldBeCreated.valueBoolean = true" -> bundle.entry as entry, entry.resource = create('Organization') as organization then 
    {
        operations then TransformOrganizationGewebediagnostikAnforderung(operations, organization, composition, section);
        operations -> entry.request as request, request.method = 'PUT', request.url = evaluate(organization, '\'Organization/\' + $this.id');
    };
}

/* ------------------------------ Organization - GewebeDiagnostik ---------------------------- */
group TransformOrganizationGewebediagnostikAnforderung(source operations: BackboneElement, target tgt: Organization, target composition: Composition, target section: BackboneElement)
{
    // Mapped profile with profile URL
    operations -> tgt.meta as meta collate, meta.profile = 'http://clinicalsite.org/fhir/StructureDefinition/organizationalunit';

    operations ->  tgt.address as address,
            address.use = 'work',
            address.type = 'postal';
    
    // Access data 
    operations.data as data then
    {
        // ID + Composition insert
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'uuid_organizationalunit_pathology'" then
        {
            values.value as value -> tgt.id = value;
            values.value as value -> section.entry = create('Reference') as organization, organization.reference = evaluate(value, '\'Organization/\' + $this');
        }; 

        // Name
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_1563'" then
        {
            values.value as value -> tgt.name = value;
        };

        // address.text
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_1562'" then
        {
            values.value as value -> tgt.address as address, address.text = value;
        };
    };
}

