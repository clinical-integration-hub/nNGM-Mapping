/// version = 0.1
/// title = "nNGM: Mapping Stammdaten FHIR"
/// CTS -> FHIR

/* 
TODO 
- Erstdiagnose -> diagnosis.condition.dateOnset : We need to create a reference to a condition for this. This should be done as soon as the ids for the individual resources are integrated.
*/

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_StammdatenFHIR" = nNGM_Mapping_StammdatenFHIR

uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as source
uses "http://hl7.org/fhir/StructureDefinition/Patient" as target
uses "http://hl7.org/fhir/StructureDefinition/Coverage" as target
uses "http://hl7.org/fhir/StructureDefinition/EpisodeOfCare" as target
uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target

group TransformBundle(source src: CTS_Transport, target bundle: Bundle)
{   
    // Metadata
    src -> bundle.id = uuid();
    src -> bundle.type = 'collection';

    // Resources
    src -> bundle.entry as entry then CreateTransformPatientStammdaten(src, entry);
    src -> bundle.entry as entry then CreateTransformCoverageStammdaten(src, entry);
    src -> bundle.entry as entry then CreateEpisodeOfCareStammdaten(src, entry);
}

/* ------------------------------ Check whether the Patient needs to be created ---------------------------- */ 
group CreateTransformPatientStammdaten(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1131'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1125'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1124'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1126'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1127'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1128'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1129'
                                        or blockindex = 0 and groupindex = 0 and itemid = 'id_1280'
                                        or blockindex = 1 and groupindex = 0 and itemid = 'id_1298'
                                        or blockindex = 1 and groupindex = 0 and itemid = 'id_2436'" then
        {
            src -> tgt.resource = create('Patient') as patient then TransformPatientStammdaten(src, patient);
        };
    };
}

/* ------------------------------ Patient ---------------------------- */
group TransformPatientStammdaten(source src: CTS_Transport, target tgt: Patient)
{
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Patient/nNGM';

    src.operations as operations, operations.data as data then
    {
        // Geschlecht -> Gender
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1131'" then
        {
            values.value as value where "$this.value = 'weiblich'" then
            {
                values.value as value -> tgt.gender = cast('female', 'FHIR.code');
            };

            values.value as value where "$this.value = 'm\u00e4nnlich' or $this.value = 'männlich'" then
            {
                values.value as value -> tgt.gender = cast('male', 'FHIR.code');
            };

            values.value as value where "$this.value = 'divers'" then
            {
                values.value as value -> tgt.gender as gender, 
                                         gender.extension as other, 
                                         other.url = 'http://fhir.de/StructureDefinition/gender-amtlich-de', 
                                         other.valueCodeableConcept = cc('http://fhir.de/ValueSet/gender-amtlich-de', 'D', 'divers');
            };
        };

        // Nachname -> name.family
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1125'" then
        {
            values.value as value -> tgt.name as name, name.family = value;
        };

        // Vorname -> name.given
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1124'" then
        {
            values.value as value -> tgt.name as name, name.given = value;
        };

        // Geburtsdatum -> birthDate
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1126'" then
        {
            values.value as value -> tgt.birthdate = dateOp(value, 'dateTime');
        };

        // Strasse -> address.line
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1127'" then
        {
            values.value as line -> tgt.address as tgtAddress, tgtAddress.line = line;
        };

        // PLZ -> address.postalcode
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1128'" then
        {
            values.value as plz -> tgt.address as tgtAddress, tgtAddress.postalCode = plz;
        };

        // Wohnort -> address.city
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1129'" then
        {
            values.value as city -> tgt.address as tgtAddress, tgtAddress.city = city;
        };

        // Country
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1280'" then
        {
            values.value as country -> tgt.address as tgtAddress, tgtAddress.country = country;
        };

        // DeceasedDateTime
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1298'" then
        {
            values.value as ddt -> tgt.deceasedDateTime = dateOp(ddt, 'dateTime');
        };

        // InformationsquelleTod
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_2436'" then
        {
            values.value as value -> tgt.extension as infoq, 
                                       infoq.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/izt',
                                       infoq.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/izt', value);
        }; 
    };
}

/* ------------------------------ Check if Coverage needs to be created ---------------------------- */ 
group CreateTransformCoverageStammdaten(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2459'
                                        or blockindex = 2 and groupindex = 0 and itemid = 'id_1269'
                                        or blockindex = 1 and groupindex = 0 and itemid = 'id_1270'
                                        or blockindex = 2 and groupindex = 0 and itemid = 'id_2460'
                                        or blockindex = 2 and groupindex = 0 and itemid = 'id_1268'" then
        {
            src -> tgt.resource = create('Coverage') as coverage then TransformCoverageStammdaten(src, coverage);
        };
    };
}

/* ------------------------------ Coverage ---------------------------- */
group TransformCoverageStammdaten(source src: CTS_Transport, target tgt: Coverage)
{ 
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Coverage/nNGM';

    //status = active
    src -> tgt.status = cast('active', 'FHIR.code'); 
    
    //beneficiary, beneficiary.reference
    src.patid as patid -> tgt.beneficiary = create('Reference') as beneficiary, beneficiary.reference = evaluate(patid, '\'Patient/\' + $this');
    
    src.operations as operations, operations.data as data then
    {
        // Krankenkasse -> coverage.payor.display
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2459'" then
        {
            values.value as value -> tgt.payor as payor, payor.display = value;
        };

        // Typ (GKV/PKV) -> type.coding
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1269'" then
        {
            values.value as type -> tgt.type = cc('http://fhir.de/ValueSet/versicherungsart-de-basis', type);
        };

        // PKV Tarif -> extension:pkvTarifform.valueCodeableConcept
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1270'" then
        {
            values.value as value where "$this.value = 'Basistarif'" -> tgt.extension as pkvTarifform,
                                        pkvTarifform.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/pkvTarifform',
                                        pkvTarifform.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/pkv-tarifform', 'basis');
            
            values.value as value where "$this.value = 'Standardtarif'" -> tgt.extension as pkvTarifform,
                                        pkvTarifform.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/pkvTarifform',
                                        pkvTarifform.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/pkv-tarifform', 'standard');
            
            values.value as value where "$this.value = 'Vollversichert'" -> tgt.extension as pkvTarifform,
                                        pkvTarifform.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/pkvTarifform',
                                        pkvTarifform.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngm/pkv-tarifform', 'voll');
        };

        // Kooperationsvereinbarung -> Coverage.extension:kooperationsvereinbarung.valueBoolean
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2460'" then
        {
            values.value as value where "value = 'yes'" then
            {
                value -> tgt.extension as koop, 
                            koop.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/kooperationsvereinbarung',
                            koop.valueBoolean = cast(true, 'FHIR.boolean');
            };

            values.value as value where "value = 'no'" then
            {
                value -> tgt.extension as koop, 
                            koop.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/kooperationsvereinbarung',
                            koop.valueBoolean = cast(false, 'FHIR.boolean');
            };
        };

        // Versichertennummer -> identifier.value
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1268'" then
        {
            values.value as value -> tgt.identifier as identifier, identifier.value = value;
        };
    };
}

/* ------------------------------ Check if EpisodeOfCare needs to be created ---------------------------- */
group CreateEpisodeOfCareStammdaten(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1297'
        or blockindex = 1 and groupindex = 0 and itemid = 'id_803'
        or blockindex = 1 and groupindex = 0 and itemid = 'id_1139'" then
        {
            src -> tgt.resource = create('EpisodeOfCare') as episodeofcare then TransformEpisodeOfCareStammdaten(src, episodeofcare);
        };
    };
}

/* ------------------------------ EpisodeOfCare ---------------------------- */
group TransformEpisodeOfCareStammdaten(source src: CTS_Transport, target tgt: EpisodeOfCare)
{
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/EpisodeOfCare/nNGM';

    // Subject
    src.patid as patid -> tgt.patient = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    // Status 
    src -> tgt.status = cast('active', 'FHIR.code');

    // Identifier 
    src -> tgt.identifier as identifier, 
            identifier.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    src.operations as operations, operations.data as data then
    {
        // TODO
        // Erstdiagnose -> diagnosis.condition.dateOnset
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1297'" then
        {
            values.value as value -> tgt.extension as erstdiagnose,
            erstdiagnose.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/nngmErstdiagnose',
            erstdiagnose.valueDateTime = dateOp(value, 'dateTime');
        };
    
        // Letzter Kontakt -> extension:letzterKontakt.dateTime
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_803'" then
        {
            values.value as value -> tgt.extension as letzterkontakt, 
            letzterkontakt.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/abrechnungsinformation/letzterKontakt',
            letzterkontakt.valueDateTime = dateOp(value, 'dateTime');
        };
 
        // NNGM status -> extension:nngmStatus.valueCodeableConcept
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1139'" then
        {
            values.value as value -> tgt.status as status,
                                     status.extension as nngmstatus, 
                                     nngmstatus.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/nngmStatus',
                                     nngmstatus.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/nngmStatus', value);
        };
   };
}